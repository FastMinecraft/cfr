plugins {
    id 'java'
    id 'maven-publish'
    id "dev.fastmc.maven-repo" version "1.0.0"
}

version = '0.3-SNAPSHOT'

group = 'net.fabricmc'
archivesBaseName = 'cfr'

sourceSets {
    main {
        java.srcDirs = ['src', 'src-templates']
    }
}

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(17)
    }
    withSourcesJar()
}

repositories {
    mavenCentral()
}

dependencies {
    implementation 'it.unimi.dsi:fastutil:8.5.11'
}

jar {
    manifest {
        attributes 'Main-Class': 'org.benf.cfr.reader.Main'
		attributes 'Implementation-Version': archiveVersion
    }
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
        }
    }
}

// A task to ensure that the version being released has not already been released.
task checkVersion {
    doFirst {
        def xml = new URL("https://maven.fabricmc.net/net/fabricmc/cfr/maven-metadata.xml").text
        def metadata = new XmlSlurper().parseText(xml)
        def versions = metadata.versioning.versions.version*.text();
        if (versions.contains(version)) {
            throw new RuntimeException("${version} has already been released!")
        }
    }
}

publish.mustRunAfter checkVersion
